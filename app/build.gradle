apply plugin: 'com.android.application'

android {

    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "fr.frogdevelopment.bibluelle"

        minSdkVersion 24
        targetSdkVersion 28

        // count nb tag to determine the current versionCode
        def cmd = 'git tag --list'
        def gitVersion = cmd.execute().text.trim().split("\n").size()

        versionCode gitVersion

        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        Properties props = new Properties()
        def propFile = new File(System.getenv("SIGNING_PROPERTIES"))
//        def propFile = new File("/home/ben/perso/signing.properties")
        if (propFile.canRead()) {
            props.load(new FileInputStream(propFile))
            if (props == null || props.isEmpty()) {
                println 'signing.properties found but entries are missing'
                debug = null
                release = null
            } else {
                debug {
                    keyAlias props['bbl_key_alias']
                    keyPassword props['bbl_key_password']
                    storePassword props['store_password']
                    storeFile file(props['store_file'])
                }
                release {
                    keyAlias props['bbl_key_alias']
                    keyPassword props['bbl_key_password']
                    storePassword props['store_password']
                    storeFile file(props['store_file'])
                }
            }
        } else {
            println 'signing.properties not found'
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
//            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:palette-v7:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    // Architecture Components
    implementation 'android.arch.lifecycle:common-java8:1.1.1'
    // Room
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    // Test helpers for Room
    androidTestImplementation 'android.arch.persistence.room:testing:1.1.1'
    // ViewModel and LiveData
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    annotationProcessor 'android.arch.lifecycle:compiler:1.1.1'
    // Test helpers for LiveData
    testImplementation 'android.arch.core:core-testing:1.1.1'

    // FIREBASE + Drive API
    implementation 'com.google.android.gms:play-services-drive:15.0.1'
    implementation 'com.google.android.gms:play-services-auth:15.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.1'

    // ZXING
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    // GSON
    implementation 'com.google.code.gson:gson:2.8.2'
    // RETROFIT
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    // https://github.com/tony19/logback-android
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    // JAVA 8 Datetime : https://github.com/JakeWharton/ThreeTenABP
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'

    // https://github.com/Clans/FloatingActionButton
    implementation 'com.github.clans:fab:1.6.4'

    // https://github.com/GrenderG/Toasty
    implementation 'com.github.GrenderG:Toasty:1.3.0'

    // https://github.com/pedant/sweet-alert-dialog
    // https://jitpack.io/p/thomper/sweet-alert-dialog/v1.4.0
    implementation 'com.github.thomper:sweet-alert-dialog:v1.4.0'

    // commons
    implementation 'org.apache.commons:commons-lang3:3.7'

    // https://github.com/Azoft/CarouselLayoutManager
    implementation 'com.azoft.carousellayoutmanager:carousel:1.2.1'

    // ProgressBar https://github.com/ybq/Android-SpinKit
    implementation 'com.github.ybq:Android-SpinKit:1.1.0'

    // https://github.com/truizlop/SectionedRecyclerView
    implementation 'com.truizlop.sectionedrecyclerview:library:1.2.0'

    // https://github.com/hdodenhof/CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // https://github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.7.1'
    implementation 'com.github.bumptech.glide:annotations:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
}

// keep it at the bottom
apply plugin: 'com.google.gms.google-services'